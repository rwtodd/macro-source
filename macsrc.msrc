#!/usr/bin/perl

use v5.030;
use feature 'signatures';
no warnings 'experimental::signatures';
use feature 'refaliasing';
no warnings 'experimental::refaliasing';

my $prefix = '@.';     # user sets this on first line of file
my $def_start = undef; # regex for definition start
my $def_end = undef;   # regex for definition end
my $def_use = undef;   # regex for fragment use

my %code_fragments;    # the collected code fragments
my $cur_frag_name;     # the fragment we are collecting
my @line_stack;        # the non-code lines to process

my %globals;           # a place where code_fragments can store values
my @result;            # the array into which code_fragments push results
my $indentation = '';  # a string with the indentation preceding a used fragment 

# ######################################################################
# Assume prefix @.  First line of the file can set PREFIX: ??
#
# Verbatim chunk definition:
# @./name goes here/
# verbatim text
# @.
#
# Perl subroutine chunk definition:
# @./name goes here/p
# push @result, 'hi there!';
# @.
#
# Raw perl code:
# @.//p
# my $counter = 1;  # anything goes!
# @.
#
# Now, inside the code, you can call on chunks like:
# void do_it(int a) {
#   @.(name goes here)
#   @.(this one has args(2, 3))
# }
# ######################################################################

sub search_state; #fwd reference

# define the parsing states we can be in
sub parse_line_state($l) {
	if($l =~ $def_start) {
		@.(parse a fragment definition)
		\&search_state;
	} else {
		push @line_stack, $l;
		\&parse_line_state;
	}
}

@./parse a fragment definition/
my ($name, $opt) = ($1, $2);
die "empty fragment name!" if(not ($name || $opt));
@.(read until end of fragment into @body)
if(not $opt) {
	push $code_fragments{$name}->@*, \@body;
} else {
    if($name) {
		my $compiled = eval ('sub { ' . (join "\n", @body) . ' }');
		push $code_fragments{$name}->@*, $compiled; 
	} else {
		# it's an anonymous code block to execute now!
		eval (join "\n", @body);
	}
}
@.

@./read until end of fragment into @body/
my @body = ();
while(<>) {
	chomp;
	last if(/$def_end/);
	push @body, $_;
}
@.

sub search_state($l) {
	if($l =~ m/^\s*$/) {
		\&search_state; 
	} else {
		parse_line_state($l);
	}
}

# step one... read the entire input
#
# the first line might be a prefix...
chomp(my $line = <>);
if($line =~ /^ \s* PREFIX: \s* (.*?) \s* $/x) {
	$prefix = $1;
	$line = '';
}
$def_start = qr{^ \s* $prefix \s* / \s* (.*?) \s* / (p?) \s* $}x;
$def_end   = qr{^ \s* $prefix \s* $}x;
$def_use   = 
	qr{^ 
		(\s*) $prefix \( \s* (.*?) \s* 
			( \( (?: (?> [^()]+ ) | (?2) )*\) )? 
		\s* \) \s*
	$}x;

# run a state machine where $st holds the next state function to run
# and each state function returns a ref to the next state
my $st = search_state($line);
while(<>) {
  chomp;
  $st = &$st($_);
}

# step two... run through the line stack, making replacements.

while(scalar @line_stack) {
	my $cur_line = shift @line_stack;
	if ($cur_line =~ /$def_use/) {
		@.(expand a used fragment)
	} else {
	  # if there's no replacement to be made, output it
	  say $cur_line;
	}
}

@./expand a used fragment/
@result = ();
$indentation = $1;
\my @frags = $code_fragments{$2} // die "Fragment <$2> not found!";
foreach my $frag (@frags) {
	if(ref($frag) eq 'CODE') {
 		eval "&\$frag$3";
	} else {
		push @result, (map { $indentation . $_ } $frag->@*);
	}
}
unshift @line_stack, @result;	
@.

# vim:ft=perl:
